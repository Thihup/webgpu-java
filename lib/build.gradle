import java.nio.file.Paths
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java-library'
    id 'maven-publish'
}

def isWindows = OperatingSystem.current().isWindows()
def wgpuVersion = project.findProperty('wgpuVersion')

configurations {
    headers
}

dependencies {
    headers project(path: ":natives", configuration: 'headers')
}

def jextractHome = project.hasProperty('jextract.home')
        ? project.property('jextract.home')
        : null

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}

jar {
    archiveBaseName = 'webgpu'
}

sourceSets {
    main {
        java {
            srcDirs += 'build/bindings'
        }
    }
}

tasks.compileJava {
    options.compilerArgs.add("--enable-preview")
}

task invokeJextract(type: Exec){
    doFirst {
        file('build/bindings').mkdirs()
    }
    def exeName = isWindows ? 'jextract.bat' : 'jextract'
    if(jextractHome){
        executable Paths.get(jextractHome as String, 'bin', exeName).toString()
    }else{
        executable exeName
    }
    args (
        configurations.headers.files[0],
        '-t', 'com.myworldvw.webgpu',
        '--output', 'build/bindings'
    )
}

task cleanupExtras(type: Delete, dependsOn: invokeJextract){
    // Delete extra files that jextract generates from included system headers that we don't
    // actually need to have. These may be different on different platforms - max_align_t.java
    // & __fsid_t.java are generated when jextract is run on Linux. WebGPU doesn't define
    // any *_t types, so we can delete these by regex.
    delete fileTree('build/bindings/com/myworldvw/webgpu/').matching {
        include '*_t.java'
    }

}

task jextract(dependsOn: [invokeJextract, cleanupExtras])

tasks.findByName('compileJava').dependsOn jextract

task sourcesJar(type: Jar, dependsOn: jextract) {
    archiveBaseName = 'webgpu'
    archiveClassifier = 'sources'
    from 'build/bindings'
}

task bindings(type: Copy, dependsOn: [jextract, jar, sourcesJar]){
    from jar
    from sourcesJar
    into 'build'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.myworldvw'
            artifactId = 'webgpu-java'
            version = "$wgpuVersion"

            artifact file("build/webgpu.jar")
            artifact sourcesJar
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = "GHPackages"
            url = uri("https://maven.pkg.github.com/MyWorldLLC/Packages")
            credentials {
                username = System.getenv("GITHUB_USER") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.password")
            }
        }
    }
}
